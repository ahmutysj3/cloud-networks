
data "google_compute_zones" "available" {
  region = var.gcp_region
}

resource "google_compute_instance" "pfsense" {
  name           = "pfsense-active1-fw"
  machine_type   = "e2-standard-4"
  zone           = data.google_compute_zones.available.names[0]
  can_ip_forward = true
  project        = var.gcp_project
  metadata = {
    ssh-keys = "trace:ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABAQCjI2kHRd2kAMmb8wbVmu66q/MfHhGiop6tZ1s7e9iJ+TzOK0S92cfIxrBTu08J6MhTg/CUfZwHe6WKB3sA5A2tWOLLpYdkvvwAojOh0z7hD9l8UZ57agRu0aaVfOofQwhQBWZFiOWIOUWmLAtHCxejV24ICJt/+pk1D+0MhqulKccC1Si7RZgzBqGzeH64mwgTbbl/QD3Hf2NcT5PvUZL9yWJDonoh1CZ5j4SfU/YJBBQXXsI3LJkH5gGCz2+CY+ZhZbtnCLrDMsgzK9uUSamdZ7bIiBi0LAM8P9O+QK75kBwnyRvQly92sIP50uxMGAfI8D/MfmHoP9pcTmHFbWcv trace@trace-laptop"
  }

  boot_disk {
    auto_delete = true
    device_name = "boot-disk"
    mode        = "READ_WRITE"
    source      = google_compute_disk.pfsense_boot.self_link
  }


  network_interface { # nic0: WAN Interface
    nic_type   = "VIRTIO_NET"
    network_ip = cidrhost(google_compute_subnetwork.hub["untrusted"].ip_cidr_range, 2)
    subnetwork = google_compute_subnetwork.hub["untrusted"].self_link

    access_config {
      nat_ip = google_compute_address.fw_external.address
    }
  }

  network_interface { # nic1: LAN Interface
    nic_type   = "VIRTIO_NET"
    network_ip = cidrhost(google_compute_subnetwork.hub["trusted"].ip_cidr_range, 2)
    subnetwork = google_compute_subnetwork.hub["trusted"].self_link
  }

  scheduling { # Discounted Rates
    automatic_restart           = false
    preemptible                 = true
    provisioning_model          = "SPOT"
    instance_termination_action = "STOP"
  }

  service_account {
    email  = data.google_compute_default_service_account.default.email
    scopes = ["cloud-platform"]
  }

  lifecycle {
    ignore_changes = [
      metadata
    ]
  }

}

data "google_compute_default_service_account" "default" {
}

resource "google_compute_address" "fw_external" {
  name         = "pfsense-wan-address"
  address_type = "EXTERNAL"
  region       = var.gcp_region
  project      = var.gcp_project

}

resource "google_compute_disk" "pfsense_boot" {
  image                     = data.google_compute_image.pfsense.self_link
  name                      = "pfsense-boot-disk"
  physical_block_size_bytes = 4096
  project                   = var.gcp_project
  size                      = 100
  type                      = "pd-standard"
  zone                      = data.google_compute_zones.available.names[0]
}

data "google_compute_image" "pfsense" {
  project     = var.gcp_project
  most_recent = true
  name        = "pfsense-partitioned-image-1"
}

resource "google_compute_instance_group" "pfsense" {
  depends_on = [google_compute_instance.pfsense]
  name       = "pfsense-instance-group"
  zone       = data.google_compute_zones.available.names[0]
  instances  = [google_compute_instance.pfsense.self_link]
}

